plugins {
	id "com.github.johnrengelman.shadow"
}

architectury {
	platformSetupLoomIde()
	forge()
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath

	forge {
		convertAccessWideners = true
		extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
	}
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
}

dependencies {
	forge "net.minecraftforge:forge:${rootProject.forge_version}"
	modApi "${rootProject.architectury_id}:architectury-forge:${rootProject.architectury_version}"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

shadowJar {
	exclude "fabric.mod.json"
	exclude "architectury.common.json"

	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
	relocate "org.apache.http", "org.teacon.libraries.org.apache.http"
	relocate "org.apache.commons.codec", "org.teacon.libraries.org.apache.commons.codec"
	relocate "org.apache.commons.logging", "org.teacon.libraries.org.apache.commons.logging"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier null
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

task moveForgeBuilds() {
	doLast {
		delete fileTree("../build/release").matching {
			include "**/Slideshow-forge*"
		}
		delete fileTree("../build/libs").matching {
			include "**/Slideshow-forge*"
		}
		delete fileTree("../build/latest").matching {
			include "**/Slideshow-forge*"
		}
		copy {
			from "build/libs"
			into "../build/release"
			rename "(.+)", "Slideshow-\$1"
		}
		copy {
			from "../build/release"
			into "../build/latest"
			rename { fileName -> fileName.replace(rootProject.mod_version, "latest") }
		}
		copy {
			from "../build/release"
			include "*dev-shadow.jar"
			into "../build/libs"
		}
		delete fileTree("../build/release").matching {
			include "*dev-shadow.jar"
		}
	}
}

afterEvaluate {
	for (def task in it.tasks) {
		if (task == tasks.build) {
			task.finalizedBy tasks.moveForgeBuilds
		}
	}
}
